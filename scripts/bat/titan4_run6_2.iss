; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppVersion "1.0.3"
#define MyAppName "titan_fil"
#define MyAppPublisher "titan_fil. All rights reserved.."
#define MyAppURL "https://test4.titannet.io/"
#define MyAppExeName "titan_fil.exe"
#define MyAppAssocName MyAppName + ""
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt

#define BuildOutputDir SourcePath + "\..\..\build\windows\x64\runner\Release"
#define LibsDir SourcePath + "\..\..\..\libs2"
#define OutputDir SourcePath + "\build"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E74359E7-7418-42A4-8A3E-D3DF1D5276B8}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\titan_fil
ChangesAssociations=yes
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=admin
OutputDir={#OutputDir}
OutputBaseFilename={#MyAppName}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
DisableDirPage=no
UsePreviousAppDir=yes
LanguageDetectionMethod=uilanguage
ShowLanguageDialog=no

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "zh"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}";


[Files]
Source: "{#BuildOutputDir}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\flutter_windows.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\screen_retriever_windows_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\system_tray_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\url_launcher_windows_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\window_manager_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\connectivity_plus_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\gol2.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BuildOutputDir}\desktop_disk_space_plugin.dll"; DestDir: "{app}"; Flags: ignoreversion

Source: "{#LibsDir}\KernelTraceControl.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\msdia140.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\msvcp140.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\vcruntime140.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\vcruntime140_1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\VC_redist.x64.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\VirtualBox-7.1.6-167084-Win.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\multipass-1.15.0+win-win64.msi"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\check_vm_names.exe"; DestDir: "{code:GetAgentInstallDir}"; Flags: ignoreversion

Source: "{#LibsDir}\isrgrootx1.der"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\lets-encrypt-r3.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\lets-encrypt-r3.der"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\goworkerd.dll"; DestDir: "{app}"; Flags: ignoreversion

Source: "{#LibsDir}\restart_exe.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\check_hypper_v.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\box_hv.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\install_multipass.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\install_vcredist.bat"; DestDir: "{app}"; Flags: ignoreversion

Source: "{#LibsDir}\install_virtualbox.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\start_main_run.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\script_sub.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\multipassDriver.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\run_agent.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\get_files_size.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\get-systemInfo.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\check_proxy_ip.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LibsDir}\titanUpdater\*"; DestDir: "{app}\titanUpdater"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "{#BuildOutputDir}\data\*"; DestDir: "{app}\data"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[CustomMessages]
; 英文消息
en.RestartQuestion=The installation has been completed. The service configuration requires a system restart to take effect. Please run the titan_fil program after restarting. Do you want to restart now?
en.RestartWarning=Notice:The Multipass service configuration requires a system restart to take effect. Please manually restart the computer later and then run the titan_fil program.
en.RestartCancelled=Restart cancelled. Please restart your computer later to complete the installation.
en.RestartLater=You chose to restart later. Please remember to restart your computer to complete the installation.
en.YesButton=Yes
en.NoButton=No
en.DirPromptLine1=Click "Next" to continue. To choose a different folder, click "Browse".
en.DirPromptLine2=⚠️ Note: The installation path MUST NOT contain Chinese characters, spaces or special characters, otherwise the node will malfunction.

; 中文消息
zh.RestartQuestion=程序安装已完成。服务配置必须重启计算机才会生效，请在重启后运行titan_fil程序。您是否确认重启？
zh.RestartWarning=提醒：服务配置必须重启计算机才会生效。请稍后手动重启计算机运行titan_fil程序.
zh.RestartCancelled=已取消重启。请稍后手动重启计算机以完成安装。
zh.RestartLater=您选择了稍后重启。请记住重启计算机以完成安装。
zh.YesButton=是
zh.NoButton=否
zh.DirPromptLine1=点击"下一步"继续。如果您想选择其它文件夹，点击"浏览"。
zh.DirPromptLine2=⚠️ 请注意：安装路径中间不能包含中文、空格及特殊字符，否则会导致节点运行异常。

[Code]


procedure InitializeWizard();
var
Line1, Line2: TNewStaticText;
BottomMargin: Integer;
begin
{ 隐藏原标签 }
WizardForm.SelectDirBrowseLabel.Visible := False;

{ 基础设置 }
BottomMargin := 10;

{ 第一行（正常字体） }
Line1 := TNewStaticText.Create(WizardForm);
Line1.Parent := WizardForm.SelectDirLabel.Parent;
Line1.Left := WizardForm.SelectDirBrowseLabel.Left;
Line1.Width := WizardForm.SelectDirBrowseLabel.Width+50;
Line1.WordWrap := True;
Line1.Font.Size :=9;

{ 第二行（粗体警告） }
Line2 := TNewStaticText.Create(WizardForm);
Line2.Parent := WizardForm.SelectDirLabel.Parent;
Line2.Left := Line1.Left;
Line2.Width := Line1.Width;
Line2.WordWrap := True;
Line2.Font.Style := [fsBold];
Line2.Font.Size := 9

{ 设置文本内容 }
if ActiveLanguage = 'en' then begin
Line1.Caption := 'Click "Next" to continue. To choose a different folder, click "Browse".';
Line2.Caption := '⚠️ Note: The path must NOT contain  spaces, or special symbols.';
end else begin
Line1.Caption := '点击"下一步"继续。如果您想选择其它文件夹，点击"浏览"。';
Line2.Caption := '⚠️ 请注意：安装路径中间不能包含中文、空格及特殊字符，否则会导致节点运行异常。';
end;

{ 计算位置 }
Line1.Height := ScaleY(20);
Line2.Height := ScaleY(20);

Line2.Top := WizardForm.NextButton.Top - BottomMargin - Line2.Height-100;
Line1.Top := Line2.Top - Line1.Height;
end;

function GetAgentInstallDir(Param: string): string;
begin
Result := ExpandConstant('{app}') + '_agent';
end;


var
NeedsRestart: Boolean;
ResultCode: Integer;

// 检查 Windows 是否是 Home 版
function IsWindowsHomeEdition: Boolean;
var
ProductName: string;
EditionID: string;
begin
Result := False;
// 检查 ProductName
if RegQueryStringValue(HKLM, 'SOFTWARE\Microsoft\Windows NT\CurrentVersion', 'ProductName', ProductName) then
begin
Result := (Pos('Home', ProductName) > 0);
end;

// 如果未通过 ProductName 检测，再检查 EditionID
if not Result then
begin
if RegQueryStringValue(HKLM, 'SOFTWARE\Microsoft\Windows NT\CurrentVersion', 'EditionID', EditionID) then
begin
Result := (Pos('Core', EditionID) > 0) or (Pos('Home', EditionID) > 0);
end;
end;
end;


// 检查 VC++ 运行时是否安装
function IsVCInstalled: Boolean;
var
regValue: Cardinal;
begin
// 检查注册表 Installed=1
if RegQueryDWordValue(HKLM, 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64', 'Installed', regValue) and (regValue = 1) then
begin
// 进一步检查关键文件是否存在
Result := FileExists(GetSystemDir + 'vcruntime140.dll');
end
else
Result := False;
end;

// 检查 VirtualBox 是否安装
function IsVirtualBoxInstalled: Boolean;
begin
Result := RegKeyExists(HKLM64, 'SOFTWARE\Oracle\VirtualBox') or
RegKeyExists(HKLM32, 'SOFTWARE\Oracle\VirtualBox');
end;

// 检查 Multipass 是否安装
function IsMultipassInstalled: Boolean;
var
ResultCode2: Integer;
begin
Result := ShellExec('', 'multipass', '--version', '', SW_HIDE, ewWaitUntilTerminated, ResultCode2) and
(ResultCode2 = 0);
end;

// 标记需要重启
procedure SetRestartNeeded;
begin
NeedsRestart := True;
end;

// 安装 VirtualBox
procedure InstallVirtualizationTool;
begin
if IsWindowsHomeEdition then
begin
// 如果是 Home 版，安装 VirtualBox
if not IsVirtualBoxInstalled then
begin
if not ShellExec('', ExpandConstant('{app}\VirtualBox-7.1.6-167084-Win.exe'), '--silent', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
begin
//MsgBox('VirtualBox installation failed!', mbError, MB_OK);
end
else
begin
SetRestartNeeded;
end;
end;
end
else
begin
// 如果不是 Home 版，执行 check_hypper_v.bat
if not ShellExec('', ExpandConstant('{app}\check_hypper_v.bat'), '', '', SW_HIDE, ewWaitUntilTerminated, ResultCode) then
begin
MsgBox('check_hypper_v.bat execution failed!', mbError, MB_OK);
end;
end;
end;

// 在安装过程中执行
procedure CurStepChanged(CurStep: TSetupStep);
var
S: string;
begin
if CurStep = ssPostInstall then
begin
// 保存选择的语言
SaveStringToFile(ExpandConstant('{app}\userlanguage.txt'), ActiveLanguage, False);
InstallVirtualizationTool;
end;

if (CurStep = ssDone) and NeedsRestart then
begin
// 第一次确认（带问号图标）
if MsgBox(ExpandConstant('{cm:RestartQuestion}'),
mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDYES then
begin
// 第一次点击"是"直接重启
Exec('shutdown', '/r /f /t 0', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
end
else
begin
// 第一次点击"否"显示第二次提示（只有一个"是"按钮）
MsgBox(ExpandConstant('{cm:RestartWarning}'),
mbError, MB_OK);
end;
end;
end;


[Run]

Filename: "{app}\lets-encrypt-r3.bat";Flags: shellexec runhidden waituntilterminated;AfterInstall: SetRestartNeeded

Filename: "{app}\VC_redist.x64.exe"; Parameters: "/install /quiet /norestart";StatusMsg: "Installing Visual C++ ..."; Check: not IsVCInstalled; AfterInstall: SetRestartNeeded; Flags: waituntilterminated;


Filename: "{app}\VirtualBox-7.1.6-167084-Win.exe";StatusMsg: "Installing VirtualBox..."; Check: IsWindowsHomeEdition and (not IsVirtualBoxInstalled);AfterInstall: SetRestartNeeded; Flags: waituntilterminated;

Filename: "msiexec.exe"; Parameters: "/i ""{app}\multipass-1.15.0+win-win64.msi";StatusMsg: "Installing Multipass..."; Check: not IsMultipassInstalled;AfterInstall: SetRestartNeeded; Flags: waituntilterminated;

